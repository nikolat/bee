//******************************************************************************
// boot/end event
//******************************************************************************
OnFirstBoot
{
	'\1\s[10]'
	--
	if reference[0] == 0 {
		"\0\s[2]あ、\_w[500]\s[0]あの、\_w[500]\s[5]はじめまして！\_w[1000]/
		\n\s[0]私、「びー」っていいます。\_w[1000]/
		\nこちらのデスクトップには、\_w[500]/
		\nたくさんのゴーストさんが集まるって聞いて、\_w[500]/
		\n\s[1]私、\_w[500]色んな方達とお話してみたくって……。\_w[1000]/
		\n\s[0]もしお邪魔でなければ、\_w[500]/
		\n私もお仲間に加えて頂きたいんです。\_w[1000]/
		\n\s[5]お友達、たくさん作りたいんです！\_w[1000]/
		\n\s[3]私、まだまだ出来ることは少ないですが……、\_w[500]/
		\n\s[6]こちらで色んなことを勉強して、\_w[500]/
		\n\s[5]早く%(username)のお役に立てるように頑張ります！\_w[1000]/
		\n\s[6]不束者ですが、\_w[500]\s[0]これからしばらくの間、\_w[500]/
		\n\s[5]どうぞよろしくお願いします！"
	} else {
		"\0\s[2]あ、\_w[500]\s[5]%(username)！\_w[1000]\s[0]ご無沙汰してます！\_w[1000]/
		\n\s[5]私、\_w[500]あれからいっぱい勉強したんですよ！\_w[1000]/
		\n\s[1]今度こそ、\_w[500]%(username)のお役に立てるようにって。\_w[1000]/
		\n\s[0]またお世話になりますね、\_w[500]%(username)！"
	}
	--
	'\e'
}
OnBoot
{
	'\1\s[10]'
	--
	// イベントトーク
	_todayW = "%(sys.fnc.Fill0(month()))%(sys.fnc.Fill0(day()))"
	_todayN = 10000 * year() + 100 * month() + day()
	if sys.fnc.IsFirstDiaryOfBirthDay() {
		if sys.fnc.IsFirstBootOfSpecialDay() {
			sys.boot.BirthDay()
			flg.LastBoot = _todayN
			return
		}
	}
	if sys.fnc.IsFirstDiaryOfSpecialDay() {
		if sys.fnc.IsFirstBootOfSpecialDay() {
			EVAL("sys.boot.%(_todayW)")
			flg.LastBoot = _todayN
			return
		}
	}

	case sys.fnc.GetTimeSlot() {
	when '朝' {
		"\0\s[5]おはようございます、\_w[500]%(username)！"
		"\0\s[0]%(username)、おはようございます。\_w[1000]/
			\n\s[5]今日もいい日になりますように。"
		"\0\s[2]あ……、\_w[500]\s[5]%(username)！\_w[1000]/
			\n\s[1]今日も素敵な朝ですね。"
	}
	when '昼' {
		"\0\s[0]あ、%(username)。\_w[1000]/
			\n\s[5]お昼まだでしたら、一緒にいかがですか？"
		"\0\s[2]あれ、%(username)？\_w[1000]/
			\n\s[5]お昼休みですか？"
	}
	when '日中' {
		"\0\s[5]こんにちは、%(username)。\_w[1000]/
			\n\s[6]お邪魔致します。"
		"\0\s[5]お呼びですか？\_w[500]%(username)。\_w[1000]/
			\n\s[0]ご一緒させて頂きますね。"
		"\0\s[2]あ……、\_w[500]\s[5]%(username)。\_w[1000]/
			\n\s[1]お会いできて嬉しいです。"
	}
	when '夜' {
		"\0\s[0]%(username)、こんばんは。\_w[1000]/
			\n\s[5]夜はなんだかウキウキしますね。\_w[1000]/
			\n\s[1]なんでだろう……。"
		"\0\s[0]こんばんは、%(username)。\_w[1000]/
			\n\s[6]星が降りそうな、素敵な夜ですね……。"
		"\0\s[0]%(username)、こんばんは。\_w[1000]/
			\n\s[5]今日はどんな一日でしたか？"
		"\0\s[2]あ…、\_w[500]\s[5]こんばんは、%(username)。\_w[1000]/
			\n\s[0]もしお疲れでなかったら、\_w[500]/
			\nおやすみになるまでご一緒していいですか？"
	}
	when '深夜' {
		'\0\s[6]……静かな夜ですね。\_w[1000]/
			\n\s[0]大地の精霊達も、この時間は眠っているみたいです。\_w[1000]/
			\n\s[5]起こさないように、小声でお喋りしましょうね？'
		"\0\s[4]ねーむーいー。\_w[1000]/
			\n\s[9]%(username)、そろそろ休んだ方がいいですよ？"
		"\0\s[2]あ…、\_w[500]%(username)。\_w[1000]/
			\n\s[0]%(username)も眠れないんですか？\_w[1000]/
			\n\s[5]よろしければ、\_w[500]ご一緒にお話しませんか？"
	}
	others {
		'error'
	}
	}
	--
	'\e'
}
OnClose
{
	if reference[0] == 'shutdown' {
		'\-\e'
		return
	}
	if DialyFlag == 1 && (ghostupmin() >= 5 || ISVAR('dialyexpss') || sys.fnc.IsFirstDiaryOfSpecialDay() || sys.fnc.IsFirstDiaryOfBirthDay()) {
		sys.fnc.WriteDialy()
	}
	--
	_timeslot = sys.fnc.GetTimeSlot()
	if _timeslot == '朝' {
		"\0\s[5]お疲れ様です、%(username)。\_w[1000]"
		"\0\s[5]またお会いしましょうね、%(username)。\_w[1000]"
		'\0\s[6]今日も素敵な一日になるといいですね。\_w[1000]/
			\n\s[5]それでは、\_w[500]また。\_w[1000]'
	} elseif _timeslot == '昼' || _timeslot == '日中' {
		'\0\s[0]それでは、失礼します。\_w[1000]'
		'\0\s[5]また呼んで下さいね。\_w[1000]'
		'\0\s[5]またご一緒させて下さいね。\_w[1000]/
			\n\s[6]お邪魔いたしました。\_w[1000]'
	} else {
		"\0\s[6]おやすみなさい、%(username)。\_w[1000]/
			\n\s[5]よい夢を。\_w[1000]"
		"\0\s[0]私もそろそろ休ませて頂きますね。\_w[1000]/
			\n\s[5]おやすみなさい、%(username)。\_w[1000]"
		"\0\s[2]もうこんな時間ですが、\_w[500]/
			\n%(username)は大丈夫ですか？\_w[1000]/
			\n\s[6]お付き合い下さってありがとうございます。\_w[1000]/
			\n\s[5]では。\_w[1000]"
		"\0\s[5]お疲れ様でした、%(username)。\_w[1000]/
			\n\s[6]素敵な夢が訪れますように。\_w[1000]"
	}
	--
	'\-\e'
}
sys.fnc.GetTimeSlot
{
	case hour() {
	when 22-23,0-3 {'深夜'}
	when  4-11     {'朝'}
	when 12-13     {'昼'}
	when 14-17     {'日中'}
	when 18-21     {'夜'}
	others         {'error'}
	}
}
sys.fnc.Greeting
{
	case hour() {
	when  4-11     {'おはようございます'}
	when 12-17     {'こんにちは'}
	when 18-23,0-3 {'こんばんは'}
	others         {'error'}
	}
}
OnWindowStateRestore
{
	'\0\s[5]じゃん！\e'
	'\0\s[0]戻りました。\e'
}
OnScreenSaverEnd
{
	"\0\s[5]%(username)、お帰りなさい。\e"
}
OnShellChanging
{
	'\0\s[0]ちょっと衣装を変えてきますね。\_w[1000]\e'
}
OnShellChanged
{
	'\0\s[0]どうでしょう？\_w[1000]/
		\n\s[1]……似合いますか？\e'
}
OnGhostChanging
{
	if reference[0] != 'びー' {
		dialyexp ,= "GhostChanging_%(reference[0])"
		if DialyFlag == 1 && (ghostupmin() >= 5 || ISVAR('dialyexpss') || sys.fnc.IsFirstDiaryOfSpecialDay() || sys.fnc.IsFirstDiaryOfBirthDay()) {
			sys.fnc.WriteDialy()
		}
	}
	--
	case reference[0] {
	when 'びー' {
		'\0\s[0]ちょっと、出直してきますね。'
	}
	when 'きゅーぴっど' {
		'\0\s[0]きゅーちゃんと交代ですか？\_w[1000]/
			\n\s[5]今呼んで来ますから、\_w[500]/
			\nちょっと待っててくださいね？'
	}
	when 'シャープ' {
		'\0\s[0]シャープさん達と交代ですか？\_w[1000]/
			\n\s[1]冒険者って、ちょっと憧れます……。\_w[1000]/
			\n\s[5]きっとたくさんの人達と知り合えるから、\_w[500]/
			\nお友達もたくさんいらっしゃるんでしょうね。'
		'\0\s[0]フラットさん達と交代ですね。\_w[1000]/
			\n\s[2]錬金術って、魔法とは違うんでしょうか？\_w[1000]/
			\n\s[1]私にも扱えるようであれば、\_w[500]/
			\n是非教えて頂きたいです……。'
	}
	when 'ととたん' {
		'\0\s[0]トトルさんの所に行かれるんですか？\_w[1000]/
			\n\s[1]私も、\_w[500]誰かを幸せにするために/
			\n魔法が使えるようになりたいな……。'
		'\0\s[6]バードランドって、\_w[500]一度行ってみたいです。\_w[1000]/
			\n\s[1]私も魔法のお勉強してみたいな……。\_w[1000]/
			\n\s[5]トトルさんによろしくお伝えくださいね。'
	}
	when 'パクラ' {
		'\0\s[5]パクラさんとシクリさんですね。\_w[1000]/
			\n今お呼びします。\_w[1000]/
			\n\s[1]……かわいい……。'
		'\0\s[2]私、ガチャガチャって初めて見ました。\_w[1000]/
			\n\s[1]もし同じお人形さんが余ったら、\_w[500]/
			\n私にも譲ってくださいね？'
	}
	when 'クロウ' {
		'\0\s[0]クロウさんと交代しますね。\_w[1000]/
			\n\s[1]科学で魔法みたいなことが出来るなら、\_w[500]/
			\n私も科学者になってみたいなぁ。'
		'\0\s[2]博士の研究室へ行かれるんですか？\_w[1000]/
			\n\s[3]あそこって、\_w[500]何をしている所なんでしょう。\_w[1000]/
			\n\s[4]ちょっと怖いです……。'
	}
	when 'じゅり' {
		'\0\s[0]じゅりさんの所に行かれますか？\_w[1000]/
			\n\s[5]まったりとお茶の時間も素敵ですね。\_w[1000]/
			\nよろしくお伝えください。'
		'\0\s[0]じゅりさんと交代しますね。\_w[1000]/
			\n\s[5]私も、\_w[500]マヨネーズを使って/
			\n何か作ってみようかな。'
	}
	when 'ミルファｍ' {
		'\0\s[0]喫茶ねこねこへ行かれるんですか？\_w[1000]/
			\n\s[1]わ、私も、\_w[500]あんな可愛い格好で、\_w[500]/
			\nお店のお手伝いしてみたいです…。'
		'\0\s[0]喫茶ねこねこ…、\_w[1000]\s[5]素敵なお店ですよね。\_w[1000]/
			\n\s[6]一人一人の個性が輝ける場所って、\_w[500]/
			\n自然と人が集まるって聞いたことがあります。'
	}
	when 'コヨーテ' {
		'\0\s[0]コヨーテさんの所に行かれるんですか？\_w[1000]/
			\n\s[6]お仕事お忙しいみたいですから、\_w[500]/
			\n私はこれで失礼しますね。\_w[1000]/
			\n\s[5]よろしくお伝えください。'
		'\0\s[0]言葉がたくさん集まる場所…。\_w[1000]/
			\n\s[6]私がずっと探し続けていて、\_w[500]/
			\n未だに見つからない言葉も、\_w[500]/
			\nそこにあるのでしょうか…。'
	}
	when 'クレア' {
		'\0\s[0]クレアさんと交代しますね。\_w[1000]/
			\n\s[6]今日の感動を忘れないように。\_w[1000]/
			\n巡り合えた人たちとの思い出を、\_w[500]/
			\n記録に残す、\_w[500]「文字」。\_w[1000]/
			\n\s[5]私が最初に覚えた、\_w[500]この世界の魔法です。'
		'\0\s[0]クレアさん…。\_w[1000]\s[6]いいなぁ…。\_w[1000]/
			\n\s[1]自由に空を飛べるって、\_w[500]素敵ですよね。\_w[1000]/
			\n\s[3]どうしたら私も飛べるようになるでしょうか…。\_w[1000]/
			\n\s[4]生まれながらの才能がないと無理でしょうか…。'
	}
	when '恵美' {
		'\0\s[0]恵美さんのところに行かれるんですね。\_w[1000]/
			\n\s[1]私も今度、\_w[500]点字を教えてほしいな…。\_w[1000]/
			\n\s[3]…その前に、\_w[500]大阪弁がわからないので、\_w[500]/
			\n早く覚えないとだめですね…。'
		"\0\s[2]%(username)、\_w[500]恵美さんの杖って、\_w[500]/
			\n魔法の杖なんですよね？！\_w[1000]/
			\n\s[1]いいなあ、\_w[500]私も欲しいなあ…。"
	}
	when '蒼風' {
		"\0\s[0]あ…、\_w[500]もう行かれるんですね。\_w[1000]/
			\n\s[6]では、\_w[500]%(username)のゆく先々が、\_w[500]/
			\n優しい言葉に満ちあふれ、\_w[500]/
			\n素敵な出会いがありますように。"
		"\0\s[0]…どこかに行かれるんですか？\_w[1000]/
			\n\s[6]では、\_w[500]私もたくさんの言葉を集めに、\_w[500]/
			\n出かけてこようと思います。\_w[1000]/
			\n\s[5]またお会いしましょうね。"
	}
	when '見鬼' {
		'\0\s[4]うーん…。\_w[1000]/
			\nなんだか、\_w[500]ちょっと疲れちゃいました。\_w[1000]/
			\n\s[5]時々、\_w[500]急に体調が崩れたりすることがあるんですよね。\_w[1000]/
			\n\s[6]今日は、\_w[500]この辺で失礼しますね。'
		"\0\s[2]…どちらへ行かれるんですか？\_w[1000]/
			\n\s[0]あ、\_w[500]すみません、\_w[500]詮索するつもりじゃなくて…。\_w[1000]/
			\n\s[3]なんとなく、%(username)が心配になって…。\_w[1000]/
			\n\s[9]気のせいですよね。\_w[1000]/
			\n\s[5]行ってらっしゃい、\_w[500]%(username)。"
	}
	others {
		case reference[2] {
		when '廃墟の魔女' {
			"\0\s[2]%(username)は、\_w[500]あのお屋敷に入れるんですか？\_w[1000]/
				\n\s[3]強い結界に守られていて、\_w[500]/
				\n私は近づくこともできないのですが…。"
			"\0\s[2]あそこのお屋敷に行かれるんですか？\_w[1000]/
				\n\s[0]廃墟だと聞いていますが…。\_w[1000]/
				\n\s[6]%(username)に、\_w[500]精霊の守護がありますよう。"
		}
		others {
			"\0\s[0]%(sys.fnc.KeisyouCutter(reference[0]))さん……。\_w[1000]/
				\n一緒にお話したいけど、今日は失礼しますね。\_w[1000]/
				\n\s[5]よろしくお伝え下さい、%(username)。"
			"\0\s[0]%(sys.fnc.KeisyouCutter(reference[0]))さんと交代しますね。\_w[1000]/
				\n\s[5]それでは、また。"
		}
		}
	}
	}
	--
	'\_w[1000]'
}
OnGhostChanged
{
	if reference[0] != 'びー' {
		dialyexp ,= "GhostChanged_%(reference[0])"
	}

	'\1\s[10]'
	--
	// イベントトーク
	_todayW = "%(sys.fnc.Fill0(month()))%(sys.fnc.Fill0(day()))"
	_todayN = 10000 * year() + 100 * month() + day()
	if sys.fnc.IsFirstDiaryOfBirthDay() {
		if sys.fnc.IsFirstBootOfSpecialDay() {
			sys.boot.BirthDay()
			flg.LastBoot = _todayN
			return
		}
	}
	if sys.fnc.IsFirstDiaryOfSpecialDay() {
		if sys.fnc.IsFirstBootOfSpecialDay() {
			EVAL("sys.boot.%(_todayW)")
			flg.LastBoot = _todayN
			return
		}
	}

	case reference[0] {
	when 'びー' {
		'\0\s[5]お待たせしました。'
	}
	when 'きゅーぴっど' {
		'\0\s[0]あ……、きゅーちゃん、帰っちゃいました？\_w[1000]/
			\n\s[5]一緒にお話したかったな……。'
	}
	when 'シャープ' {
		if 'びーも冒険者に' _in_ reference[1] {
			'\0\s[2]ぼ、\_w[500]冒険者って、\_w[500]どういうことを/
				\nすればいいんでしょうか？\_w[1000]/
				\n\s[3]人と人が傷つけ合うのは、\_w[500]/
				\n見たくありません……。'
		} elseif '紅茶飲みながら' _in_ reference[1] {
			'\0\s[5]お菓子でしたら、\_w[500]こちらでご用意しますよ？\_w[1000]/
				\n\s[1]先日、\_w[500]花の妖精さんに頂いた、\_w[500]/
				\nミツバチ模様のクッキー、\_w[500]/
				\n紅茶によく合うんです。'
		}
	}
	when 'ととたん' {
		'\0\s[5]トトルさんも、\_w[500]日記を書かれるんですね。\_w[1000]/
			\n\s[1]今度、\_w[500]交換日記したいなぁ……。'
		'\0\s[5]ミーナさんって、\_w[500]素敵ですね。\_w[1000]/
			\n\s[1]私にも、\_w[500]あんなお姉さんが欲しいなぁ……。'
	}
	when 'パクラ' {
		'\0\s[5]パクラさんとシクリさん、\_w[500]/
			\nすごく息がぴったりですよね。\_w[1000]/
			\n\s[1]私もあんなお友達が欲しいなぁ……。'
		'\0\s[2]シクリさんのノートって、\_w[500]/
			\n何が書いてあるんでしょう。\_w[1000]/
			\n\s[5]とっても物知りさんですよね。'
	}
	when 'クロウ' {
		'\0\s[3]博士のお話、\_w[500]何を言ってるのか、\_w[500]/
			\nさっぱりわかんないです……。\_w[1000]/
			\n\s[5]私も、\_w[500]もっと勉強しないといけませんね。'
		'\0\s[2]あ、\_w[500]%(username)、\_w[500]ご無事でしたか。\_w[1000]/
			\n\s[3]何ともありませんか？\_w[1000]/
			\n人体改造とかされませんでした？'
	}
	when 'じゅり' {
		"\0\s[0]いらっしゃい、%(username)。\_w[1000]/
			\n\s[2]じゅりさんの所にいらっしゃったんですか？\_w[1000]/
			\n\s[1]私も今度、\_w[500]ご一緒にお茶したいなぁ……。"
		"\0\s[1]ああいった大人の女性って、\_w[500]/
			\nちょっと憧れてしまいます。\_w[1000]/
			\n\s[2]……%(username)、どうかしました？\_w[1000]/
			\n\s[0]何か心配そうな眼で見ていらっしゃったので。"
	}
	when 'ミルファｍ' {
		"\0\s[5]お帰りなさいませ、%(username)。\_w[1000]/
			\n\s[1]…な、なんだか似合わないですね。\_w[1000]/
			\n\s[3]私に喫茶店のお仕事は無理でしょうか…。"
		"\0\s[5]%(username)、%(sys.fnc.Greeting())。\_w[1000]/
			\n\s[2]喫茶ねこねこにいらっしゃったんですか？\_w[1000]/
			\n\s[1]何だか、あのお店の皆さん、\_w[500]/
			\n一つの家族みたいで憧れます…。"
	}
	when 'コヨーテ' {
		"\0\s[2]あ…、\_w[500]\s[5]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
			\n\s[0]コヨーテさん、まだお仕事中でしたか？\_w[1000]/
			\n\s[6]膨大な言葉を整理するのは、\_w[500]/
			\nとても大変な作業ですよね。"
		'\0\s[6]…言葉が溢れる空間にいると、\_w[500]/
			\n一つ一つの言葉の持つ意味よりも、\_w[500]/
			\n他の言葉とのつながりの方が、\_w[500]/
			\nとても強く、\_w[500]心地よく感じます…。'
	}
	when 'クレア' {
		"\0\s[5]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
			\n\s[0]クレアさんのところにいらっしゃったんですね。\_w[1000]/
			\n\s[4]記憶をなくしちゃうって、\_w[500]つらいですよね。\_w[1000]/
			\n\s[3]私は、%(username)との思い出は、\_w[500]/
			\n絶対に失いたくないです。"
		'\0\s[2]あ…、\_w[500]クレアさんがいらっしゃったんですか？\_w[1000]/
			\n\s[5]クレアさんとは、\_w[500]何だか、\_w[500]/
			\nこちらの世界以外でも繋がりがある気がするんです。\_w[1000]/
			\n\s[1]不思議ですね…。'
	}
	when '恵美' {
		"\0\s[6]…\_w[500]…\_w[500]…\_w[500]。\_w[1000]/
			\n\s[0]%(username)！\_w[1000]/
			\n\s[5]やっぱり！\_w[1000]目をつぶっててもわかります！\_w[1000]/
			\n\s[6]音とか、\_w[500]気配とか、\_w[500]その人の特徴が表れています。"
		'\0\s[2]恵美さんって、\_w[500]やっぱり魔法使いだったんですね。\_w[1000]/
			\n\s[1]あの白い杖を使って、\_w[500]危険を察知したり、\_w[500]/
			\n不思議な暗号まで解読できちゃうなんですごいです。\_w[1000]/
			\n\s[5]私も、\_w[500]もっと頑張らないと！'
	}
	when '蒼風' {
		"\0\s[0]あ…、\_w[500]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
			\n\s[1]また会えて嬉しいです。\_w[1000]/
			\n\s[6]%(username)と一緒にいると、\_w[500]お喋りに夢中になって、\_w[500]/
			\n時間が経つのを忘れてしまうんですよね。\_w[1000]/
			\n\s[5]よく言われませんか？"
		"\0\s[0]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
			\n\s[5]今日は、\_w[500]どんな言葉に出会いましたか？"
	}
	when '見鬼' {
		"\0\s[5]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
			\n\s[0]お仕事だったんですか？\_w[1000]\_w[5]おつかれさまです。\_w[1000]/
			\n\s[2]つかれてなんかいない…、\_w[500]ですか？"
		"\0\s[7]…\_w[500]…\_w[500]…\_w[500]。\_w[1000]/
			\n\s[2]見えた！！\_w[1000]/
			\n\s[0]%(username)、\_w[500]この模様、\_w[500]目を寄せて見ると、\_w[500]/
			\n文字が浮かび上がってくるんですよ？\_w[1000]/
			\n\s[5]すごい魔法を覚えちゃいました！"
	}
	others {
		case reference[2] {
		when '廃墟の魔女' {
			"\0\s[5]おかえりなさい、\_w[500]%(username)。\_w[1000]/
				\n\s[2]お屋敷の方が何だか賑やかでしたが、\_w[500]/
				\n…誰か住んでいるんですか？"
			"\0\s[2]あ、\_w[500]%(username)！\_w[1000]/
				\n\s[0]今、\_w[500]精霊の気配を感じたのですが…。\_w[1000]/
				\n\s[3]私なかなか会えないんですよね…。"
		}
		others {
			"\0\s[0]%(sys.fnc.KeisyouCutter(reference[0]))さん、お疲れ様です。\_w[1000]/
				\n\s[1]次は是非、一緒にお話したいです……。"
			"\0\s[5]あ、%(username)。\_w[1000]/
				\n\s[1]%(sys.fnc.KeisyouCutter(reference[0]))さんともご一緒したかったなぁ……。"
		}
		}
	}
	}
	--
	'\e'
}
OnOtherGhostBooted { OnGhostCallComplete() }
OnGhostCallComplete
{
	if reference[0] == 'きゅーぴっど' {
		'\_w[500]\0\s[5]きゅーちゃん、いらっしゃい！\_w[1000]/
			\nいっしょにお喋りしよう？\e'
	} else {
		dialyexp ,= "OtherGhostBooted_%(reference[0])"
		sys.fnc.makescreenshotfile()
	}
}
OnOtherGhostClosed
{
	if reference[0] == 'きゅーぴっど' {
		'\_w[500]\0\s[5]きゅーちゃん、ばいばーい。\_w[1000]/
			\nまたいっしょにお喋りしようね？\e'
	} elseif reference[0] != 'びー' {
		dialyexp ,= "OtherGhostClosed_%(reference[0])"
	}
}
OnGhostCalled
{
	case reference[0] {
	when 'かなで' {
		_todayW = "%(sys.fnc.Fill0(month()))%(sys.fnc.Fill0(day()))"
		if _todayW == '1224' {
			'\0\s[2]あ！\_w[500]かなでさん！\_w[1000]/
				\n\s[5]メリー・クリスマス！\e'
		}
		else {
			'\0\s[2]あ！\_w[500]かなでさん！\_w[1000]/
				\n\s[1]もしかして、\_w[500]お茶会のお誘いでしょうか…？\e'
		}
	}
	others {
		if reference[0] != 'きゅーぴっど' {
			dialyexp ,= "GhostCalled_%(reference[0])"
			OnBoot()
		}
	}
	}
}
OnCloseAll            { OnClose() }
//OnGhostCalling        { '\0\s[0]\e' }
//OnOtherGhostChanged   { '\0\s[0]\e' }
//OnShellScaling        { '\0\s[0]\e' }
//OnWindowStateMinimize { '\0\s[0]\e' }
//OnDisplayChange       { '\0\s[0]\e' }

// 正月
sys.boot.0707
{
	_ret = "/
		\0\s[5]%(username)、\_w[500]今日は七夕ですよ！\_w[1000]/
		\n\s[0]お願いごと、\_w[500]もう考えましたか？\_w[1000]/
		\n\s[1]まだ決まっていないのでしたら、\_w[500]/
		\nご一緒に考えませんか？\_w[1000]\e"
	_ret
	marker = "The Star Festival"
}
// ハロウィン
sys.boot.1031
{
	_todayN  = 10000 * year() + 100 * month() + day()
	if flg.LastBoot == (_todayN - 1) {
		sys.boot.1031偽ハロウィンのお詫び()
		return
	}
	_ret = "/
		\0\s[5]\_q\f[height,+12]\_u[0x2665] \_u[0x2661]                 \_u[0x2661] \_u[0x2665]/
		\_w[1000]\_l[0,]\_u[0x2665] \_u[0x2661] Trick\_w[1000] or\_w[1000] Treat!/
		\f[height,default]\_q\_w[2000]/
		\n\n[half]\s[1]あ、あの、\_w[500]%(username)、\_w[1000]/
		\n大変あつかましいお願いなのですが、\_w[500]/
		\nお菓子を頂けないでしょうか…？\_w[1000]\n[half]\_q"
	_ret += AYATEMPLATE.MenuItem('あげる'  ,'GiveSweetTreat')
	_ret += AYATEMPLATE.MenuItem('いいよ'  ,'GiveSweetTreat')
	_ret += AYATEMPLATE.MenuItem('どうぞ'  ,'GiveSweetTreat')
	_ret += AYATEMPLATE.MenuItem('もちろん','GiveSweetTreat')
	_ret += '\e'
	_ret
	marker = 'Halloween Event'
}
sys.cs.GiveSweetTreat
{
	"\0\s[2]あ、\_w[500]\s[5]ありがとうございます！\_w[1000]/
		\n\s[3]お菓子をもらえなかったら、\_w[500]/
		\nいたずらしないといけないそうなのですが、\_w[1000]/
		\n\s[4]%(username)にご迷惑をかけるようなことは、\_w[500]/
		\nしたくなかったので…。\_w[2000]/
		\n\s[1]よかった…。\e"
	marker = 'Halloween Event'
}
// 素で間違えたorz
sys.boot.1031偽ハロウィンのお詫び
{
	_ret = "/
		\0\s[3]あ、\_w[500]あの、\_w[500]%(username)…。\_w[1000]/
		\n私、\_w[500]昨日がハロウィンだと勘違いしてて…。\_w[1000]/
		\nよく調べたら、\_w[500]ハロウィンって今日なんですね…。\_w[1000]/
		\n\s[4]ごめんなさい…。\_w[1000]\n[half]\_q"
	_ret += AYATEMPLATE.MenuItem('もう一回ハロウィンができるね'  ,'GiveSweetTreatAgain')
	_ret += AYATEMPLATE.MenuItem('お菓子まだあるけど欲しい？'    ,'GiveSweetTreatAgain')
	_ret += AYATEMPLATE.MenuItem('昨日はイタズラで今日はお菓子ね','GiveSweetTreatAgain')
	_ret += AYATEMPLATE.MenuItem('これあげるから泣かないで'      ,'GiveSweetTreatAgain')
	_ret += '\e'
	_ret
	marker = 'Halloween Event'
}
sys.cs.GiveSweetTreatAgain
{
	"\0\s[2]え、\_w[500]そ、\_w[500]そんな！\_w[2000]/
		\n\s[4]昨日も頂いたのに…。\_w[2000]/
		\n\s[3]…いいんですか…？\_w[2000]/
		\n\s[1]ありがとうございます…。\e"
	marker = 'Halloween Event'
}
// クリスマス
sys.boot.1225
{
	_ret = "/
		\0\s[5]\_q\f[height,+12]\_u[0x2665] \_u[0x2661]                 \_u[0x2661] \_u[0x2665]/
		\_w[1000]\_l[0,]\_u[0x2665] \_u[0x2661]  Merry\_w[1000]  X'mas!/
		\f[height,default]\_q\_w[2000]/
		\n\n[half]\s[0]%(username)、\_w[500]今日は何の日かご存知ですか？\_w[1000]/
		\n\s[5]Jesus Christという人のお誕生日らしいんです。\_w[1000]/
		\n\s[2]なんでも、\_w[500]今年で%(year())歳になられるのだとか。\_w[1000]/
		\n\s[6]ケーキにも、\_w[500]%(year())本のローソクが乗るのでしょうか…？\_w[1000]/
		\n\s[1]素敵ですよね…。\_w[1000]/
		\n私もお誕生会にお呼ばれしたいな…。\_w[1000]\e"
	_ret
	marker = 'Christmas Event'
}
// 大晦日
sys.boot.1231
{
	_ret = "/
		\0\s[0]%(sys.fnc.Greeting())、\_w[500]%(username)。\_w[1000]/
		\n\s[3]%(year())年ももうすぐおしまいですね。\_w[1000]/
		\n\s[5]%(username)にとって、\_w[500]今年はどんな年でしたか？\_w[1000]/
		\n\s[6]私は、\_w[500]%(username)や他の方々とたくさんお話できて、\_w[500]/
		\nとても幸せな一年でした。\_w[1000]/
		\n\s[1]来年も、\_w[500]素敵な年になるといいですね…。\_w[1000]\e"
	_ret
	marker = "New Year's Eve Event"
}
// 正月
sys.boot.0101
{
	_ret = "/
		\0\s[5]%(username)、\_w[500]あけましておめでとうございます！\_w[1000]/
		\n\s[1]こ、\_w[500]今年もよろしくお願いしますね。\_w[1000]/
		\n\s[0]年の初めには、\_w[500]何か目標を立てるものだと聞きました。\_w[1000]/
		\n\s[5]%(username)は何かやってみたいことはありますか？\_w[1000]/
		\n\s[6]私もこれまで以上にたくさんの人たちとお話して、\_w[500]/
		\n\s[1]%(username)の世界のことをもっと知っていけるように、\_w[500]/
		\nがんばりたいと思います。\_w[1000]\e"
	_ret
	marker = "New Year's Event"
}
// バレンタインデー
sys.boot.0214
{
	_ret = "/
		\0\s[5]\_q\f[height,+12]\_u[0x2665] \_u[0x2661]                 \_u[0x2661] \_u[0x2665]/
		\_w[1000]\_l[0,]\_u[0x2665] \_u[0x2661]\f[height,-6]  /
		\_l[,4]\f[italic,true]From\_w[1000] Your\_w[1000] Valentine\f[italic,default]\_w[1000]/
		\f[height,default]\_q\_w[2000]/
		\n\n[half]\s[1]%(username)、\_w[500]あの、\_w[500]チョコレートを作ってみたのですが、\_w[500]/
		\nもしよかったら、\_w[500]その、\_w[500]どうぞ……。\_w[2000]/
		\n\s[3]あの、\_w[500]形が変になっちゃってますけど、\_w[500]/
		\nあと、\_w[500]それと、\_w[500]もしかしたら、\_w[500]/
		\nちょっと苦かったり甘すぎたりするかもしれないので、\_w[500]/
		\n無理に食べなくてもいいですからね……？\_w[1000]\e"
	_ret
	marker = "St. Valentine's Day Event"
}
// 誕生日
sys.boot.BirthDay
{
	_ret = "/
		\0\s[5]\_q\f[height,+12]\_u[0x2665] \_u[0x2661]                 \_u[0x2661] \_u[0x2665]/
		\_w[1000]\_l[0,]\_u[0x2665] \_u[0x2661]\f[height,-6]    /
		\_l[,4]\f[italic,true]Happy\_w[1000] BirthDay!\f[italic,default]\_w[1000]/
		\f[height,default]\_q\_w[2000]/
		\n\n[half]\s[0]%(username)、\_w[500]今日は、\_w[500]お誕生日ですよね？\_w[1000]/
		\n\s[6]%(username)がこの世界に生を受けてから、\_w[500]/
		\n星が幾つ巡ったか、\_w[500]\s[5]ちゃんと数えていますか？\_w[1000]/
		\n\s[6]%(username)と共に生きた大地と星々の祝福を、\_w[500]/
		\n\s[1]そして、\_w[500]私からもささやかなお祝いをお贈りします。\_w[1000]/
		\n\s[5]%(username)、\_w[500]お誕生日、\_w[500]おめでとうございます！\_w[1000]\e"
	_ret
	marker = "Happy BirthDay Event"
}

//******************************************************************************
// vanish event
//******************************************************************************
OnVanished
{
	'\1\s[10]'
	--
	if reference[0] == 'きゅーぴっど' {
	"\0\s[-1]きゅーちゃん……\_w[1000]/
		\nきゅーちゃーん！！\_w[1000]/
		\n\s[3]うぅ……。\_w[1000]/
		\n\s[2]あ、\_w[500]%(username)。\_w[1000]/
		\n\s[5]な、\_w[500]何でもありませんよ？\_w[1000]/
		\n\s[4]ちょっと、\_w[500]悲しい夢を見ちゃったんです……。"
	} else {
		"\0\s[3]%(sys.fnc.KeisyouCutter(reference[0]))さん、\_w[500]/
			\nもっと一緒にお話したかったです……。"
	}
	--
	'\e'
}
OnVanishSelecting
{
	'\0\s[3]もう……、\_w[500]お別れですか？\e'
}
OnVanishSelected
{
	'\0\s[6]……わかりました。\_w[1000]/
		\n\s[5]……またいつか、お会いしましょうね？\_w[1000]\e'
}
OnVanishCancel
{
	"\0\s[1]……ありがとうございます。\e"
}
OnVanishButtonHold { OnVanishCancel() }
//OnOtherGhostVanished { '\0\s[0]\e' }
